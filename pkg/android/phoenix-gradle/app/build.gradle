apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.retroarch"
        minSdkVersion 16
        targetSdkVersion 28

        ndk {
            moduleName "retroarch-activity"
        }

        externalNativeBuild {
            ndkBuild {
                arguments "-j${Runtime.runtime.availableProcessors()}"
            }
        }
    }

    flavorDimensions "abi"
    productFlavors {
        ra32 {
            dimension "abi"
            ndk {
                abiFilters 'armeabi-v7a', 'x86'
            }
            resValue "string", "app_name", "RetroArch"
        }
        ra64 {
            dimension "abi"
            ndk {
                abiFilters 'arm64-v8a', 'x86_64'
            }
            applicationIdSuffix '.aarch64'
            resValue "string", "app_name", "RetroArch (AArch64)"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    externalNativeBuild {
        ndkBuild {
            path file('../../phoenix/jni/Android.mk')
        }
    }

    sourceSets {
        ra32 {
            java.srcDirs = ['../../phoenix/src']
            res.srcDirs = ['../../phoenix/res', '../../phoenix/libs/googleplay/res']
        }
        ra64 {
            java.srcDirs = ['../../phoenix/src']
            res.srcDirs = ['../../phoenix64/res', '../../phoenix64/libs/googleplay/res']
        }
    }
}

dependencies {
    implementation files('../../phoenix/libs/googleplay/libs/google-play-services.jar')
}
